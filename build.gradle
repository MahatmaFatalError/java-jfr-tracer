apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: 'jdks.gradle'

repositories {
	mavenCentral()
}

sourceSets {
	java11 {
		java {
			srcDirs = ['src/main/java11']
		}
	}
}

dependencies {
	compile group: 'io.opentracing', name: 'opentracing-api', version: '0.31.0'
	compile group: 'io.jaegertracing', name: 'jaeger-core', version: '0.32.0'
	compile group: 'io.opentracing.brave', name: 'brave-opentracing', version: '0.31.2'
	java11Compile group: 'io.opentracing', name: 'opentracing-api', version: '0.31.0'
	java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
	testImplementation 'junit:junit:4.12'
}

compileJava {
	options.compilerArgs.addAll(['-Xlint:all,-deprecation'])
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

compileJava11Java {
	options.compilerArgs.addAll(['--release', '11', '-Xlint:all'])
	sourceCompatibility = 11
	targetCompatibility = 11
}

jar {
	into('META-INF/versions/11') {
		from sourceSets.java11.output
	}
	manifest.attributes(
		'Multi-Release': 'true', 
	)
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
	include '**DelegatingJfrTracer**'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact sourcesJar
			artifact javadocJar
			groupId = 'io.opentracing.contrib'
			artifactId = 'jfr-tracer'
			
			from components.java
			
			pom {
				name = 'JFR Tracer'
				description = 'An OpenTracing Tracer which records contextual information into the JDK Flight Recorder'
				url = 'https://github.com/opentracing-contrib/java-jfr-tracer'

				licenses {
					license {
						name = 'Apache v2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
 					}
				}

				developers {
					developer {
						id = 'thegreystone'
						name = 'Marcus Hirt'
						email = 'marcus@hirt.se'
					}
					developer {
						id = 'sfriberg'
						name = 'Staffan Friberg'
						email = 'sfriberg@kth.se'
					}
				}

				scm {
					connection = 'scm:git:https://github.com/opentracing-contrib/java-jfr-tracer.git'
					developerConnection = 'scm:git:https://github.com/opentracing-contrib/java-jfr-tracer.git'
					url = 'https://github.com/opentracing-contrib/java-jfr-tracer'
				}
			}
		}
	}
}

def ossrhUsername = project.findProperty('ossrhUsername') ?: ''
def ossrhPassword = project.findProperty('ossrhPassword') ?: ''

if(ossrhUsername?.trim() && ossrhPassword?.trim()){
	apply plugin: 'signing'

	repositories {
		maven {
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username ossrhUsername
				password ossrhPassword
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}
